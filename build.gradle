plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'com.github.spotbugs' version '6.0.26'
	id 'net.ltgt.errorprone' version '4.1.0'
}

group = 'de.eisingerf'
version = '0.0.1-SNAPSHOT'

// Fixed bug WebSecurityConfig(debug=true) throws error
// https://github.com/spring-projects/spring-security/issues/14370
ext['spring-security.version']='6.2.2'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'

	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	compileOnly 'org.projectlombok:lombok:1.18.34'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	testRuntimeOnly 'com.h2database:h2:2.2.224'

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'

	errorprone('com.google.errorprone:error_prone_core:2.29.0')
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		removeUnusedImports("cleanthat-javaparser-unnecessaryimport")
		cleanthat()
		importOrder()
		palantirJavaFormat('2.39.0').formatJavadoc(true)
		formatAnnotations()
	}
}
